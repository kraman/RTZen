options {
  LOOKAHEAD = 1;
  CHOICE_AMBIGUITY_CHECK = 2;
  OTHER_AMBIGUITY_CHECK = 1;
  STATIC = false;
  DEBUG_PARSER = false;
  DEBUG_LOOKAHEAD = false;
  DEBUG_TOKEN_MANAGER = false;
  ERROR_REPORTING = true;
  JAVA_UNICODE_ESCAPE = false;
  UNICODE_INPUT = false;
  IGNORE_CASE = false;
  USER_TOKEN_MANAGER = false;
  USER_CHAR_STREAM = false;
  BUILD_PARSER = true;
  BUILD_TOKEN_MANAGER = true;
  SANITY_CHECK = true;
  FORCE_LA_CHECK = false;
}

PARSER_BEGIN(IDLParser)

package idldepend.javacc;

/**
 * Changes on the jj file included with the JavaCC 2.1 distribution,
 * to reflect the changes on CORBA 2.3.
 */
public class IDLParser {

  public static void main(String args[]) {
    IDLParser parser;
    if (args.length == 0) {
      System.out.println("IDL Parser Version 0.1:  Reading from standard input . . .");
      parser = new IDLParser(System.in);
    } else if (args.length == 1) {
      System.out.println("IDL Parser Version 0.1:  Reading from file " + args[0] + " . . .");
      try {
        parser = new IDLParser(new java.io.FileInputStream(args[0]));
      } catch (java.io.FileNotFoundException e) {
        System.out.println("IDL Parser Version 0.1:  File " + args[0] + " not found.");
        return;
      }
    } else {
      System.out.println("IDL Parser Version 0.1:  Usage is one of:");
      System.out.println("         java IDLParser < inputfile");
      System.out.println("OR");
      System.out.println("         java IDLParser inputfile");
      return;
    }
    try {
      parser.specification();
      System.out.println("IDL Parser Version 0.1:  Java program parsed successfully.");
    } catch (ParseException e) {
      System.out.println("IDL Parser Version 0.1:  Encountered errors during parse.");
      System.out.println(e);
    }
  }

}

PARSER_END(IDLParser)


/*
 * Tokens to ignore in the BNF follow.
 */

SKIP :
{
  < " " >
| < "\t" >
| < "\n" >
| < "\r" >
| < "//" (~["\n"])* "\n" >
| <"/*" (~["*"])* ("*")+ (~["/","*"] (~["*"])* ("*")+)* "/">
| < "#" ([" ","\t"])* (["0"-"9"])+
    (([" ","\t"])* "\"" (~["\""])+ "\""
           ([" ","\t"])* (["0"-"9"])* ([" ","\t"])* (["0"-"9"])*)? "\n" >
}

/* Production 1 */

void specification() :
{}
{
  ( definition() )+ <EOF>
}

/* Production 2 */

void definition() :
{}
{
  type_dcl() ";"
|
  const_dcl() ";"
|
  except_dcl() ";"
|
  LOOKAHEAD(2)
  interfacex() ";"
|
  module() ";"
|
  value() ";"
}

/* Production 3 */

void module() :
{}
{
  "module" identifier() "{" ( definition() )+ "}"
}

/* Production 4 */

void interfacex() :
{}
{
  LOOKAHEAD(4)
  interface_dcl()
|
  forward_dcl()
}

/* Production 5 */

void interface_dcl() :
{}
{
  interface_header() "{" interface_body() "}"
}

/* Production 6 */

void forward_dcl() :
{}
{
  [ "abstract" ] "interface" identifier()
}

/* Production 7 */

void interface_header() :
{}
{
  [ "abstract" ] "interface" identifier() [ inheritance_spec() ]
}

/* Production 8 */

void interface_body() :
{}
{
  ( export() )*
}

/* Production 9 */

void export() :
{}
{
  type_dcl() ";"
|
  const_dcl() ";"
|
  except_dcl() ";"
|
  attr_dcl() ";"
|
  op_dcl() ";"
}

/* Production 10 */

void inheritance_spec() :
{}
{
  ":" interface_name() ( "," interface_name() )*
}

/* Production 11 */

void interface_name() :
{}
{
  scoped_name()
}

/* Production 12 */

void scoped_name() :
{}
{
  [ "::" ] identifier() ( "::" identifier() )*
}

/* Production 13 */

void value() :
{
}
{
  LOOKAHEAD(3)
  value_dcl()
|
  LOOKAHEAD(3)
  value_abs_dcl()
|
  LOOKAHEAD(3)
  value_box_dcl()
|
  value_forward_dcl()
}

/* Production 14 */

void value_forward_dcl() :
{
}
{
  [ "abstract" ] "valuetype" identifier()
}

/* Production 15 */

void value_box_dcl() :
{
}
{
  "valuetype" identifier() type_spec()
}

/* Production 16 */

void value_abs_dcl() :
{
}
{
  "abstract" "valuetype"
  identifier() value_inheritance_spec() "{" ( export() )* "}"
}

/* Production 17 */

void value_dcl() :
{
}
{
  value_header() "{" ( value_element() )* "}"
}

/* Production 18 */

void value_header() :
{
}
{
  [ "custom" ] "valuetype" identifier() value_inheritance_spec()
}

/* Production 19 */

void value_inheritance_spec() :
{
}
{
  [ ":" [ "truncatable" ] value_name() ( "," value_name() )* ]
  [ "supports" interface_name() ( "," interface_name() )* ]
}

/* Production 20 */

void value_name() :
{
}
{
  scoped_name()
}

/* Production 21 */

void value_element() :
{
}
{
  ( export() | state_member() | init_dcl() )
}

/* Production 22 */

void state_member() :
{
}
{
  ( "public" | "private" ) type_spec() declarators() ";"
}

/* Production 23 */

void init_dcl() :
{
}
{
  "factory" identifier() "(" [ init_param_decls() ] ")" ";"
}

/* Production 24 */

void init_param_decls() :
{
}
{
  init_param_decl() ( "," init_param_decl() )*
}

/* Production 25 */

void init_param_decl() :
{
}
{
  init_param_attribute() param_type_spec() simple_declarator()
}

/* Production 26 */

void init_param_attribute() :
{
}
{
  "in"
}

/* Production 27 */

void const_dcl() :
{}
{
  "const" const_type() identifier() "=" const_exp()
}

/* Production 28 */

void const_type() :
{}
{
  LOOKAHEAD(2)
  integer_type()
|
  char_type()
|
  wide_char_type()
|
  boolean_type()
|
  floating_pt_type()
|
  string_type()
|
  wide_string_type()
|
  fixed_pt_const_type()
|
  scoped_name()
|
  octet_type()
}

/* Production 29 */

void const_exp() :
{}
{
  or_expr()
}

/* Production 30 */

void or_expr() :
{}
{
  xor_expr() ( "|" xor_expr() )*
}

/* Production 31 */

void xor_expr() :
{}
{
  and_expr() ( "^" and_expr() )*
}

/* Production 32 */

void and_expr() :
{}
{
  shift_expr() ( "&" shift_expr() )*
}

/* Production 33 */

void shift_expr() :
{}
{
  add_expr() ( ( ">>" | "<<" ) add_expr() )*
}

/* Production 34 */

void add_expr() :
{}
{
  mult_expr() ( ( "+" | "-" ) mult_expr() )*
}

/* Production 35 */

void mult_expr() :
{}
{
  unary_expr() ( ( "*" | "/" | "%" ) unary_expr() )*
}

/* Production 36 */

void unary_expr() :
{}
{
  [ unary_operator() ] primary_expr()
}

/* Production 37 */
void unary_operator() :
{}
{
  "-"
|
  "+"
|
  "~"
}

/* Production 38 */

void primary_expr() :
{}
{
  scoped_name()
|
  literal()
|
  "(" const_exp() ")"
}

/* Production 39 */

void literal() :
{}
{
  integer_literal()
|
  string_literal()
|
  wide_string_literal()
|
  character_literal()
|
  wide_character_literal()
|
  fixed_pt_literal()
|
  floating_pt_literal()
|
  boolean_literal()
}

/* Production 40 */

void boolean_literal() :
{}
{
  "TRUE"
|
  "FALSE"
}

/* Production 41 */

void positive_int_const() :
{}
{
  const_exp()
}

/* Production 42 */

void type_dcl() :
{}
{
  "typedef" type_declarator()
|
  struct_type()
|
  union_type()
|
  enum_type()
|
  "native" simple_declarator()
}

/* Production 43 */

void type_declarator() :
{}
{
  type_spec() declarators()
}

/* Production 44 */

void type_spec() :
{}
{
  simple_type_spec()
|
  constr_type_spec()
}

/* Production 45 */

void simple_type_spec() :
{}
{
  base_type_spec()
|
  template_type_spec()
|
  scoped_name()
}

/* Production 46 */

void base_type_spec() :
{}
{
  LOOKAHEAD(2)
  floating_pt_type()
|
  integer_type()
|
  char_type()
|
  wide_char_type()
|
  boolean_type()
|
  octet_type()
|
  any_type()
|
  object_type()
|
  value_base_type()
}

/* Production 47 */

void template_type_spec() :
{}
{
  sequence_type()
|
  string_type()
|
  wide_string_type()
|
  fixed_pt_type()
}

/* Production 48 */

void constr_type_spec() :
{}
{
  struct_type()
|
  union_type()
|
  enum_type()
}

/* Production 49 */

void declarators() :
{}
{
  declarator() ( "," declarator() )*
}

/* Production 50 */

void declarator() :
{}
{
  LOOKAHEAD(2)
  complex_declarator()
|
  simple_declarator()
}

/* Production 51 */

void simple_declarator() :
{}
{
  identifier()
}

/* Production 52 */

void complex_declarator() :
{}
{
  array_declarator()
}

/* Production 53 */

void floating_pt_type() :
{}
{
  "float"
|
  "double"
|
  "long" "double"
}

/* Production 54 */

void integer_type() :
{}
{
  signed_int()
|
  unsigned_int()
}

/* Production 55 */

void signed_int() :
{}
{
  signed_short_int()
|
  LOOKAHEAD(2)
  signed_long_int()
|
  signed_longlong_int()
}

/* Production 56 */

void signed_short_int() :
{}
{
  "short"
}

/* Production 57 */

void signed_long_int() :
{}
{
  "long"
}

/* Production 58 */

void signed_longlong_int() :
{}
{
  "long" "long"
}

/* Production 59 */

void unsigned_int() :
{}
{
  LOOKAHEAD(2)
  unsigned_short_int()
|
  LOOKAHEAD(3)
  unsigned_long_int()
|
  unsigned_longlong_int()
}

/* Production 60 */

void unsigned_short_int() :
{}
{
  "unsigned" "short"
}

/* Production 61 */

void unsigned_long_int() :
{}
{
  "unsigned" "long"
}

/* Production 62 */

void unsigned_longlong_int() :
{}
{
  "unsigned" "long" "long"
}

/* Production 63 */

void char_type() :
{}
{
  "char"
}

/* Production 64 */

void wide_char_type() :
{}
{
  "wchar"
}

/* Production 65 */

void boolean_type() :
{}
{
  "boolean"
}

/* Production 66 */

void octet_type() :
{}
{
  "octet"
}

/* Production 67 */

void any_type() :
{}
{
  "any"
}

/* Production 68 */

void object_type() :
{}
{
  "Object"
}

/* Production 69 */

void struct_type() :
{}
{
  "struct" identifier() "{" member_list() "}"
}

/* Production 70 */

void member_list() :
{}
{
  ( member() )+
}

/* Production 71 */

void member() :
{}
{
  type_spec() declarators() ";"
}

/* Production 72 */

void union_type() :
{}
{
  "union" identifier() "switch" "(" switch_type_spec() ")" "{" switch_body() "}"
}

/* Production 73 */

void switch_type_spec() :
{}
{
  integer_type()
|
  char_type()
|
  boolean_type()
|
  enum_type()
|
  scoped_name()
}

/* Production 74 */

void switch_body() :
{}
{
  ( casex() )+
}

/* Production 75 */

void casex() :
{}
{
  ( case_label() )+ element_spec() ";"
}

/* Production 76 */

void case_label() :
{}
{
  "case" const_exp() ":"
|
  "default" ":"
}

/* Production 77 */

void element_spec() :
{}
{
  type_spec() declarator()
}

/* Production 78 */

void enum_type() :
{}
{
  "enum" identifier() "{" enumerator() ( "," enumerator() )* "}"
}

/* Production 79 */

void enumerator() :
{}
{
  identifier()
}

/* Production 80 */

void sequence_type() :
{}
{
  "sequence" "<" simple_type_spec() [ "," positive_int_const() ] ">"
}

/* Production 81 */

void string_type() :
{}
{
  "string" [ "<" positive_int_const() ">" ]
}

/* Production 82 */

void wide_string_type() :
{}
{
  "wstring" [ "<" positive_int_const() ">" ]
}

/* Production 83 */

void array_declarator() :
{}
{
  identifier() ( fixed_array_size() )+
}

/* Production 84 */

void fixed_array_size() :
{}
{
  "[" positive_int_const() "]"
}

/* Production 85 */

void attr_dcl() :
{}
{
  [ "readonly" ] "attribute" param_type_spec() simple_declarator() ( "," simple_declarator() )*
}

/* Production 86 */

void except_dcl() :
{}
{
  "exception" identifier() "{" ( member() )* "}"
}

/* Production 87 */

void op_dcl() :
{}
{
  [ op_attribute() ] op_type_spec() identifier() parameter_dcls() [ raises_expr() ] [ context_expr() ]
}

/* Production 88 */

void op_attribute() :
{}
{
  "oneway"
}

/* Production 89 */

void op_type_spec() :
{}
{
  param_type_spec()
|
  "void"
}

/* Production 90 */

void parameter_dcls() :
{}
{
  "(" [ param_dcl() ( "," param_dcl() )* ] ")"
}

/* Production 91 */

void param_dcl() :
{}
{
  param_attribute() param_type_spec() simple_declarator()
}

/* Production 92 */

void param_attribute() :
{}
{
  "in"
|
  "out"
|
  "inout"
}

/* Production 93 */

void raises_expr() :
{}
{
  "raises" "(" scoped_name() ( "," scoped_name() )* ")"
}

/* Production 94 */

void context_expr() :
{}
{
  "context" "(" string_literal() ( "," string_literal() )* ")"
}

/* Production 95 */

void param_type_spec() :
{}
{
  base_type_spec()
|
  string_type()
|
  wide_string_type()
|
  scoped_name()
}

/* Production 96 */

void fixed_pt_type() :
{}
{
  "fixed" "<" positive_int_const() "," positive_int_const() ">"
}

/* Production 97 */

void fixed_pt_const_type() :
{}
{
  "fixed"
}

/* Production 98 */

void value_base_type() :
{}
{
  "ValueBase"
}

/* Definitions of complex regular expressions follow */

void identifier() :
{}
{
  <ID>
}

void integer_literal() :
{}
{
  <OCTALINT>
|
  <DECIMALINT>
|
  <HEXADECIMALINT>
}

void string_literal() :
{}
{
  <STRING>
}

void wide_string_literal() :
{}
{
  <WSTRING>
}

void character_literal() :
{}
{
  <CHARACTER>
}

void wide_character_literal() :
{}
{
  <WCHARACTER>
}

void floating_pt_literal() :
{}
{
  <FLOATONE>
|
  <FLOATTWO>
}

void fixed_pt_literal() :
{}
{
  <FIXED>
}

TOKEN :
{
  <  ID : ["a"-"z","A"-"Z", "_"] (["a"-"z","A"-"Z","0"-"9","_"])* >
| <  OCTALINT : "0" (["0"-"7"])* (["u","U","l","L"])? >
| <  DECIMALINT : ["1"-"9"] (["0"-"9"])* (["u","U","l","L"])? >
| <  HEXADECIMALINT : ("0x"|"0X") (["0"-"9","a"-"f","A"-"F"])+ (["u","U","l","L"])? >
| <  FIXED : ((["0"-"9"])+ (".")? (["0"-"9"])* | (["0"-"9"])* (".")? (["0"-"9"])+)
   ( "d" | "D" )>
| <  FLOATONE : ((["0"-"9"])+ "." (["0"-"9"])* | (["0"-"9"])* "." (["0"-"9"])+)
   (["e","E"] (["-","+"])? (["0"-"9"])+)? (["f","F","l","L"])? >
| <  FLOATTWO : (["0"-"9"])+ ["e","E"] (["-","+"])?
   (["0"-"9"])+ (["f","F","l","L"])? >
| <  WCHARACTER : "L" "'"
   (   (~["'","\\","\n","\r"])
   | ("\\" (
             ["n","t","v","b","r","f","a","\\","?","'","\""]
            |
             "0" (["0"-"7"])*
            |
             ["1"-"9"] (["0"-"9"])*
            |
             ("0x" | "0X") (["0"-"9","a"-"f","A"-"F"])+
            |
             "u" (["0"-"9","a"-"f","A"-"F"])+
           )
     )
   )
   "'" >
| <  CHARACTER : "'"
   (   (~["'","\\","\n","\r"])
   | ("\\" (
             ["n","t","v","b","r","f","a","\\","?","'","\""]
            |
             "0" (["0"-"7"])*
            |
             ["1"-"9"] (["0"-"9"])*
            |
             ("0x" | "0X") (["0"-"9","a"-"f","A"-"F"])+
           )
     )
   )
   "'" >
| < WSTRING : "L" "\""
   ( ( ~["\"","\\","\n","\r"])
   | ("\\" (
             ["n","t","v","b","r","f","a","\\","?","'","\""]
            |
             "0" (["0"-"7"])*
            |
             ["1"-"9"] (["0"-"9"])*
            |
             ("0x" | "0X") (["0"-"9","a"-"f","A"-"F"])+
            |
             "u" (["0"-"9","a"-"f","A"-"F"])+
           )
     )
   )*
   "\"" >
| <  STRING : "\""
   ( ( ~["\"","\\","\n","\r"])
   | ("\\" (
             ["n","t","v","b","r","f","a","\\","?","'","\""]
            |
             "0" (["0"-"7"])*
            |
             ["1"-"9"] (["0"-"9"])*
            |
             ("0x" | "0X") (["0"-"9","a"-"f","A"-"F"])+
           )
     )
   )*
   "\"" >
}
